apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion build_versions.compileSdk
    buildToolsVersion build_versions.buildTools

    defaultConfig {
        applicationId "com.king.mvvm.component"
        minSdkVersion build_versions.minSdk
        targetSdkVersion build_versions.targetSdk
        versionCode app_version.versionCode
        versionName app_version.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

        //在 BuildConfig 中添加自定义参数 isBuildModule ，用于判断各个子 Module 是否是独立运行，来做相应的逻辑处理
        buildConfigField "Boolean","isBuildModule", "$rootProject.ext.isBuildModule"
    }

    //统一规范资源名称前缀，防止多个 module 之间资源冲突
    resourcePrefix "app_"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }


    }

    buildFeatures{
        dataBinding = true
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }


    lintOptions {
        abortOnError false
        warning 'InvalidPackage'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation deps.test.junit
    androidTestImplementation deps.test.android_junit
    androidTestImplementation deps.test.runner
    androidTestImplementation deps.test.espresso

    //lifecycle
    kapt "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle"
    //room
    kapt "androidx.room:room-compiler:$versions.room"
    //hilt
    implementation "com.google.dagger:hilt-android:$versions.hiltAndroid"
    kapt "com.google.dagger:hilt-android-compiler:$versions.hiltAndroid"

    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$versions.hilt"
    kapt "androidx.hilt:hilt-compiler:$versions.hilt"
    //router
    kapt deps.router.arouter_compiler

    //当各个 module 作为 application 独立运行时，宿主app需引入使用到的一些公共依赖
    if (rootProject.ext.isBuildModule){
        implementation deps.constraintlayout

        implementation deps.kotlin_stdlib
        implementation deps.kotlin_core

        implementation deps.kotlinx_coroutines_core
        implementation deps.kotlinx_coroutines_android
        implementation deps.lifecycle_viewmodel_ktx

        implementation project(":common-base")

    }else{

        //当各个module 作为 library 时，合并到宿主app
        implementation project(":module-joke")
        implementation project(":module-news")
    }

    //leakCanary
    debugImplementation deps.leakcanary

}

